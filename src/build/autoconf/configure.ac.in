## 
## This file is part of RGL
##
## use src/build/Maintainer.mk to update configure.ac
## Process this file with autoconf to produce a configure script.
## origin: src/build/autoconf/configure.ac.in
##
## $Id: configure.ac.in,v 1.10 2003/11/22 08:37:45 dadler Exp $
##

AC_PREREQ(2.50)


## ---[ VERSION ]-------------------------------------------------------------

AC_INIT([rgl], [@VERSION@], [dadler@uni-goettingen.de])
AC_CONFIG_AUX_DIR(src/build/autoconf)

## ---[ check host ]----------------------------------------------------------

AC_CANONICAL_HOST

case "${host_os}" in
  cygwin*|mingw*|windows*|winnt)
    echo "Windows port: use R CMD INSTALL to build (configure.win)"
    exit -1
  ;;
  *)
  ;;
esac


##
## ===[ X11 Port ]============================================================
##

## C language

CPPFLAGS=""
CFLAGS="-Wall -pedantic"


## C++ language

LIBS="-lstdc++"
CXXFLAGS="${CFLAGS} -fno-exceptions -fno-rtti"


## X11 backend

AC_PATH_X
if test x$no_x == xyes ; then
  AC_MSG_ERROR([X11 not found, configure aborted.
reason: RGL requires X11 on non-win32 platforms.])
fi

LDFLAGS="${LDFLAGS} -L${x_libraries}"
LIBS="${LIBS} -lX11 -lXext"
CPPFLAGS="${CPPFLAGS} -I${x_includes}"

## OpenGL backend

AC_ARG_WITH(gl-includes,
[  --with-gl-includes=DIR  specify location of OpenGL headers],
[CPPFLAGS="${CPPFLAGS} -I${withval}"]
)

AC_ARG_WITH(gl-libs,
[  --with-gl-libs=DIR      specify location of OpenGL libs],
[LIBS="${LIBS} -L${withval}"]
)

AC_ARG_WITH(gl-libname,
[  --with-gl-libname=NAME specify Library name (defaults to "GL")], 
[LIBS="${LIBS} -l${withval}"], [LIBS="${LIBS} -lGL"]
)

AC_ARG_WITH(glu-libname,
[  --with-glu-libname=NAME specify GLU Library name (defaults to "GLU")],
[LIBS="${LIBS} -l${withval}"], [LIBS="${LIBS} -lGLU"]
)

## LibPNG support

AC_ARG_ENABLE([libpng],
[  --disable-libpng           compile without PNG image support]
)
AC_MSG_RESULT([$enable_libpng])

if test "x$enable_libpng" != "xno"; then

  AC_ARG_ENABLE([libpng-config],
  [  --disable-libpng-config    disable libpng-config test and configuration]
  )
  if test "x$enable_libpng_config" != "xno"; then
    AC_CHECK_PROG([HAVE_LIBPNG_CONFIG],[libpng-config],[yes],[no])
  fi

  if test "x$HAVE_LIBPNG_CONFIG" == "xyes" ; then
    CPPFLAGS="${CPPFLAGS} -DHAVE_PNG_H `libpng-config --cppflags`"
    CFLAGS="${CFLAGS} `libpng-config --cflags`"
    LDFLAGS="${LDFLAGS} `libpng-config --ldflags`"
    LIBS="${LIBS} `libpng-config --libs`"
  else    
    TEST_DIRS="/usr/local /usr"
    for I in $TEST_DIRS ; do
      AC_MSG_CHECKING([libpng in $I])
      if ( test -f $I/lib/libpng.so || test -f $I/lib/libpng.a ) && 
         ( test -f $I/include/png.h ) ; then
        CPPFLAGS="${CPPFLAGS} -DHAVE_PNG_H -I${I}/include"
        LDFLAGS="${LDFLAGS} -L${I}/lib"
        LIBS="${LIBS} -lpng"
        AC_MSG_RESULT([found])
      else
        AC_MSG_RESULT([not found])
      fi
    done
  fi
 
fi

## Darwin/MacOS X stuff

if test `uname` = "Darwin" ; then
  CPPFLAGS="${CPPFLAGS} -DDarwin"
  CFLAGS="${CFLAGS} -DDarwin"
  # Apple's OpenGL is different from the X11 one - it must be loaded *before* X11
  if test -e /System/Library/Frameworks/OpenGL.framework ; then
    CPPFLAGS="-I/System/Library/Frameworks/OpenGL.framework/Headers ${CPPFLAGS} -DNO_GL_PREFIX"
    LIBS="-framework OpenGL ${LIBS}"
  fi
  # X11 must come *after* the OpenGL stuff
  CPPFLAGS="${CPPFLAGS} -I/usr/X11R6/include"
fi

## Output

## AC_SUBST(PLATFORM_MODULES)
AC_SUBST(CPPFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LIBS)
AC_SUBST(DEFS)
    
AC_OUTPUT(src/Makevars)
